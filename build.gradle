apply plugin: 'java'

repositories {
    jcenter()
}

dependencies {
    def versions = [
            jackson : '2.11.0',
            jaxb    : '2.3.1',
            jersey  : '2.31',
            jetty   : '9.4.30.v20200611',
            logback : '1.2.3',
            oshi    : '5.2.0',
            restEasy: '4.5.5.Final',
            slf4j   : '1.7.30'
    ]

    // System monitor
    compile group: 'com.github.oshi', name: 'oshi-core', version: versions.oshi

    // JSON
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: versions.jackson
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: versions.jackson
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: versions.jackson
    compile group: 'com.fasterxml.jackson.dataformat', name: 'jackson-dataformat-xml', version: versions.jackson

    // REST/web server
    compile group: 'org.jboss.resteasy', name: 'resteasy-client', version: versions.restEasy
    compile group: 'org.glassfish.jersey.core', name: 'jersey-server', version: versions.jersey
    compile group: 'org.glassfish.jersey.inject', name: 'jersey-hk2', version: versions.jersey
    compile group: 'org.glassfish.jersey.containers', name: 'jersey-container-servlet-core', version: versions.jersey
    compile group: 'org.glassfish.jersey.containers', name: 'jersey-container-jetty-http', version: versions.jersey
    compile group: 'org.glassfish.jersey.media', name: 'jersey-media-json-jackson', version: versions.jersey
    compile group: 'org.glassfish.jersey.media', name: 'jersey-media-multipart', version: versions.jersey
    compile group: 'org.eclipse.jetty', name: 'jetty-server', version: versions.jetty
    compile group: 'org.eclipse.jetty', name: 'jetty-servlet', version: versions.jetty
    compile group: 'javax.xml.bind', name: 'jaxb-api', version: versions.jaxb

    // Logging
    compile group: 'org.slf4j', name: 'slf4j-api', version: versions.slf4j
    compile group: 'ch.qos.logback', name: 'logback-core', version: versions.logback
    compile group: 'ch.qos.logback', name: 'logback-classic', version: versions.logback
}

jar {
    manifest {
        attributes "Main-Class": "com.github.typingtanuki.servermonitor.MonitorMain"
    }

    from {
        configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

task tmpPack(type: Copy, dependsOn: ['jar']) {
    destinationDir = file('build/dist')
    from tasks.jar

    into('.') {
        from("${project(':').projectDir}/script")
        fileMode 0755
    }

    into('conf') {
        from("${project(':').projectDir}/conf")
        exclude("monitor.json")
        exclude("key.pri")
    }
    into('www/dist') {
        from("${project(':').projectDir}/www/dist")
        exclude "*.map"
    }
    into('www') {
        from("${project(':').projectDir}/www/index.html")
        from("${project(':').projectDir}/www/monitor.css")
    }
}

task pack(type: Zip) {
    into("serverMonitor") {
        dependsOn tmpPack
        with tmpPack
    }
}