plugins {
    id 'java'
    id 'application'
    id 'net.ltgt.errorprone' version '2.0.0'
}

repositories {
    jcenter()
}

configurations {
    distJar.extendsFrom implementation
}

def versions = [
        errorProne: '2.5.1',
        jackson   : '2.12.2',
        jaxb      : '2.3.1',
        jersey    : '3.0.1',
        jetty     : '11.0.1',
        logback   : '1.2.3',
        oshi      : '5.6.1',
        restEasy  : '4.6.0.Final',
        slf4j     : '1.7.30',
        junit     : '4.13'
]
dependencies {

    errorprone("com.google.errorprone:error_prone_core:${versions.errorProne}")

    // System monitor
    implementation group: 'com.github.oshi', name: 'oshi-core', version: versions.oshi

    // JSON
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: versions.jackson
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: versions.jackson
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: versions.jackson
    implementation group: 'com.fasterxml.jackson.dataformat', name: 'jackson-dataformat-xml', version: versions.jackson

    // REST/web server
    implementation group: 'org.jboss.resteasy', name: 'resteasy-client', version: versions.restEasy
    implementation group: 'org.glassfish.jersey.core', name: 'jersey-server', version: versions.jersey
    implementation group: 'org.glassfish.jersey.inject', name: 'jersey-hk2', version: versions.jersey
    implementation group: 'org.glassfish.jersey.containers', name: 'jersey-container-servlet-core', version: versions.jersey
    implementation group: 'org.glassfish.jersey.containers', name: 'jersey-container-jetty-http', version: versions.jersey
    implementation group: 'org.glassfish.jersey.media', name: 'jersey-media-json-jackson', version: versions.jersey
    implementation group: 'org.glassfish.jersey.media', name: 'jersey-media-multipart', version: versions.jersey
    implementation group: 'org.eclipse.jetty', name: 'jetty-server', version: versions.jetty
    implementation group: 'org.eclipse.jetty', name: 'jetty-servlet', version: versions.jetty
    implementation group: 'javax.xml.bind', name: 'jaxb-api', version: versions.jaxb

    // Logging
    implementation(group: 'org.slf4j', name: 'slf4j-api', version: versions.slf4j) {
        version {
            strictly versions.slf4j
        }
        force = true
    }
    implementation group: 'ch.qos.logback', name: 'logback-core', version: versions.logback
    implementation group: 'ch.qos.logback', name: 'logback-classic', version: versions.logback

    // Test
    testImplementation group: 'junit', name: 'junit', version: versions.junit
}

jar {
    manifest {
        attributes "Main-Class": "com.github.typingtanuki.servermonitor.MonitorMain"
    }

    from {
        // Fat jar
        configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
        configurations.distJar.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

task tmpPack(type: Copy, dependsOn: ['jar']) {
    destinationDir = file('build/dist')
    from tasks.jar

    into('.') {
        from("${project(':').projectDir}/script")
        exclude("*.ps1")
        fileMode 0755
    }

    into('conf') {
        from("${project(':').projectDir}/conf")
        exclude("monitor.json")
        exclude("key.pri")
    }

    into('script') {
        from("${project(':').projectDir}/script")
        exclude("monitor.*")
        fileMode 0755
    }

    into('www/dist') {
        from("${project(':').projectDir}/www/dist")
        exclude "*.map"
    }
    into('www') {
        from("${project(':').projectDir}/www/index.html")
        from("${project(':').projectDir}/www/monitor.css")
    }
}

task pack(type: Zip) {
    into("serverMonitor") {
        dependsOn tmpPack
        with tmpPack
    }
}